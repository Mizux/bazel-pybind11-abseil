module(
    name = "bazel-pybind11-abseil",
    version = "head",
)

# Only direct dependencies need to be listed below.
# Please keep the versions in sync with the versions in the WORKSPACE file.
# see https://registry.bazel.build/
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.2.9")
bazel_dep(name = "rules_python", version = "1.6.3")
bazel_dep(name = "abseil-cpp", version = "20250814.0")
bazel_dep(name = "abseil-py", version = "2.1.0")
bazel_dep(name = "pybind11_bazel", version = "3.0.0")
bazel_dep(name = "pybind11_abseil", version = "202402.0")

git_override(
    module_name = "pybind11_abseil",
    commit = "70f8b693b3b70573ca785ef62d9f48054f45d786",
    patch_strip = 1,
    patches = ["//patches:pybind11_abseil.patch"],
    remote = "https://github.com/pybind/pybind11_abseil.git",
)

#### DEV ONLY DEPENDENCIES BELOW HERE ####
bazel_dep(name = "googletest", version = "1.17.0")

SUPPORTED_PYTHON_VERSIONS = [
    "3.14",
    "3.13",
    "3.12",
    "3.11",
    "3.10",
    "3.9",
]

DEFAULT_PYTHON = "3.13"
python = use_extension("@rules_python//python/extensions:python.bzl", "python", dev_dependency = True)

[
    python.toolchain(
        ignore_root_user_error = True,  # needed for CI
        is_default = version == DEFAULT_PYTHON,
        python_version = version,
    )
    for version in SUPPORTED_PYTHON_VERSIONS
]

use_repo(python, python = "python_versions")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip", dev_dependency = True)

[
    pip.parse(
        hub_name = "bp11_absl_pip_deps",
        python_version = version,
        requirements_lock = "//bazel:requirements.txt",
    )
    for version in SUPPORTED_PYTHON_VERSIONS
]

use_repo(pip, pypi = "bp11_absl_pip_deps")
